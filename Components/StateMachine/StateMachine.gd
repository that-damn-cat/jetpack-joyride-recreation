@icon("./gears.svg")
class_name StateMachine
extends Node

signal state_changed(old: State, new: State)
@export var initial_state: State
var states: Dictionary[StringName, State] = { }
var current_state: State
var previous_state: State


func _enter_tree() -> void:
	states = { }

	for child in get_children():
		if child is State:
			add_state(child)


func _ready() -> void:
	if initial_state:
		_set_state(initial_state)

	var state_children: Array[State] = []
	for child in get_children():
		if child is State:
			state_children.append(child)

	if state_children.size() == 0:
		push_warning("StateMachine has no State children at ready!")

	var state_names := {}
	var duplicates := ""

	for state in state_children:
		var this_name := state.state_name

		if state_names.has(this_name):
			duplicates += "- '%s'\n" % this_name
		else:
			state_names[this_name] = true

	if not duplicates == "":
		push_error("Duplicate state_name values detected:\n" + duplicates)


func _process(delta: float) -> void:
	if current_state:
		current_state.update(delta)


func _physics_process(delta: float) -> void:
	if current_state:
		current_state.physics_update(delta)


## Returns the state object with the given name
func get_state(state_name : String) -> State:
	return(states.get(state_name))


## Adds a given state to the State Machine
func add_state(new_state : State) -> void:
	new_state.state_machine = self

	# The new state must be a direct child of the StateMachine.
	if new_state.get_parent() != self:
		new_state.get_parent().remove_child(new_state)
		add_child(new_state)

	if states.has(new_state.state_name):
		push_warning("Duplicate state_name '%s' detected." % new_state.state_name)

	states[new_state.state_name] = new_state
	new_state.transitioned.connect(_on_state_transitioned)

## Removes a given state from the State Machine, leaving it in the tree.
## If the removed state is the current state, transitions to the previous state.
## If there is no previous state, transitions to the initial state.
## Finally, if there is no initial state, the StateMachine will have no current state.
func remove_state(state_to_remove: State) -> void:
	if not states.has(state_to_remove.state_name):
		return

	# Remove references to the removed state in previous/initial
	if state_to_remove == previous_state:
		previous_state = null

	if state_to_remove == initial_state:
		initial_state = null

	# If we are removing the current state, try to handle it gracefully.
	if state_to_remove == current_state:
		if previous_state:
			change_state(previous_state.state_name)
		elif initial_state:
			change_state(initial_state.state_name)
		else:
			current_state = null

	states.erase(state_to_remove.state_name)
	state_to_remove.transitioned.disconnect(_on_state_transitioned)


## Returns whether the State Machine has a state with the given name.
func has_state(state_name: String) -> bool:
	return states.has(state_name)


## Forces a state change to the given state name.
func change_state(new_state_name: String) -> void:
	var new_state := get_state(new_state_name)

	if not new_state:
		push_error("State '%s' does not exist in the state machine." % new_state_name)
		return

	_set_state(new_state)


func _on_state_transitioned(this_state: State, new_state_name: String) -> void:
	if this_state != current_state:
		push_error("State transition signal received from a state that is not the current state.")
		push_error("Current State: %s" % current_state)
		push_error("Signal received from: %s" % this_state)
		return

	var new_state: State = get_state(new_state_name)
	if !new_state:
		push_error("State '%s' does not exist in the state machine." % new_state_name)
		return

	_set_state(new_state)


func _set_state(new_state: State) -> void:
	if not new_state:
		current_state = null
		return

	if new_state == current_state:
		return

	if current_state:
		current_state.exit()
		previous_state = current_state

	current_state = new_state
	current_state.enter()
	state_changed.emit(previous_state, current_state)
